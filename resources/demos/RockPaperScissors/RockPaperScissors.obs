contract Player {
    state ToChoose;
    state Rock;
    state Paper;
    state Scissors;

    Player() {
        ->ToChoose;
    }

    transaction choose(Player@Owned >> Unowned this, string choice) returns Player@(Rock | Paper | Scissors) {
        if (choice == "rock") {
            ->Rock;
        } else {
            if (choice == "paper") {
                ->Paper;
            } else {
                if (choice == "scissors") {
                    ->Scissors;
                } else {
                    revert "choice must be one of: rock, paper, scissors";
                }
            }
        }

        return this;
    }
}

main contract RockPaperScissors {
    state GameStart;
    state PlayerOneChose;
    state PlayerTwoChose;
    state PlayerOneWin;
    state PlayerTwoWin;
    state Tie;

    Player@(Rock | Paper | Scissors) playerOne available in PlayerOneChose, PlayerTwoChose;
    Player@(Rock | Paper | Scissors) playerTwo available in PlayerTwoChose;

    RockPaperScissors() {
        ->GameStart;
    }

    transaction playerOneChoose(RockPaperScissors@GameStart >> PlayerOneChose this, string choice) {
        ->PlayerOneChose(playerOne = new Player().choose(choice));
    }

    transaction playerTwoChoose(RockPaperScissors@PlayerOneChose >> PlayerTwoChose this, string choice) {
        ->PlayerTwoChose(playerTwo = new Player().choose(choice));
    }

    transaction computeWinner(RockPaperScissors@PlayerTwoChose >> (PlayerOneWin | PlayerTwoWin | Tie) this) {
        if (playerOne in Rock) {
            if (playerTwo in Rock) {
                ->Tie;
            } else {
                if (playerTwo in Scissors) {
                    ->PlayerOneWin;
                } else {
                    ->PlayerTwoWin;
                }
            }
        } else {
            if (playerOne in Scissors) {
                if (playerTwo in Scissors) {
                    ->Tie;
                } else {
                    if (playerTwo in Paper) {
                        ->PlayerOneWin;
                    } else {
                        ->PlayerTwoWin;
                    }
                }
            } else {
                if (playerTwo in Paper) {
                    ->Tie;
                } else {
                    if (playerTwo in Rock) {
                        ->PlayerOneWin;
                    } else {
                        ->PlayerTwoWin;
                    }
                }
            }
        }
    }

    transaction reset(RockPaperScissors@Owned >> GameStart this) {
        ->GameStart;
    }
}

