import "resources/case_studies/Insurance/PolicyRecord.obs"


// This implements a mutable linked list of Pending policies.
contract PendingPolicyList {
    asset state HasNext {
        PendingPolicyList@Owned next;
    }

    asset state NoNext;
    state Empty;

    Policy@Unowned key available in HasNext, NoNext;
    PolicyRecord@Pending val available in HasNext, NoNext;


    PendingPolicyList@Empty() {
        ->Empty;
    }

    transaction getKey(PendingPolicyList@HasNext this) returns Policy@Unowned {
        return key;
    }

    transaction getValue(PendingPolicyList@HasNext this) returns PolicyRecord@Unowned {
        return val;
    }

    transaction disconnectNext(PendingPolicyList@HasNext >> NoNext this) returns PendingPolicyList@Owned {
        PendingPolicyList removedNext = next;
        ->NoNext;
        return removedNext;
    }

    transaction reconnectNext(PendingPolicyList@NoNext >> HasNext this, PendingPolicyList@Owned >> Unowned newNext) {
        ->HasNext(next = newNext);
    }

    private transaction removeValue(PendingPolicyList@NoNext >> Empty this) returns PolicyRecord@Pending {
        PolicyRecord valCopy = val;
        ->Empty;
        return valCopy;
    }


    transaction append(PendingPolicyList@Owned this, Policy@Unowned k, PolicyRecord@Pending >> Unowned v) {
        switch this {
            case HasNext {
                next.append(k, v);
            }
            case Empty {
                ->HasNext(key = k, val = v, next = new PendingPolicyList());
            }
        }
    }



    // Removes prevNode.next, returning the PolicyRecord that was previously owned by prevNode.next.
    // Precondition: this is not the last node of the linked list.
    transaction removeNext(PendingPolicyList@HasNext this) returns PolicyRecord@Pending {
        PendingPolicyList removedNode = this.next;
        [removedNode@Owned];

        switch removedNode {
            case Empty {
                throw; // this case doesn't make any sense.
            }
            case HasNext {
                this.next = removedNode.disconnectNext();
                [removedNode@NoNext];
                return removedNode.removeValue();
            }
        }
    }

    // Assumes "this" is not a match.
     transaction removeInRest(PendingPolicyList@Owned this, Policy@Unowned policy) returns PolicyRecord@Pending {
          switch this {
               case Empty {
                   throw; // We didn't find the policy in the list.
               }
               case HasNext {
                   switch next {
                        case Empty {
                            throw;
                        }
                        case HasNext {
                            if(next.getKey().equals(policy)) {
                                return this.removeNext();
                            }
                            else {
                                return next.removeInRest(policy);
                            }
                        }
                   }
               }
          }
    }
}


main asset contract PendingPolicyMap {
    PendingPolicyList@Owned list;

    PendingPolicyMap@Owned() {
        list = new PendingPolicyList(); // Start with an empty list.
    }

    transaction set(PendingPolicyMap@Owned this, Policy@Unowned key, PolicyRecord@Pending >> Unowned val) {
        list.append(key, val);
    }




    // remove and return value of the pair with the given policy as a key
    transaction get(PendingPolicyMap@Owned this, Policy@Unowned policy) returns PolicyRecord@Pending {
        PolicyRecord removed;

        switch list {
            case Empty {
                throw; // Element not in map.
            }
            case HasNext {
                if(list.getKey().equals(policy)) {
                    PendingPolicyList secondNode = list.disconnectNext();
                    [list@NoNext];
                    removed = list.removeValue();
                    list = secondNode;
                } else {
                    removed = list.removeInRest(policy);
                }
            }
        }

        return removed;

    }
}
